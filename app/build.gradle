import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version "$ksp_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.compose' version "$kotlin_version"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

android {
    namespace = 'com.hegocre.nextcloudpasswords'
    compileSdk = 36

    defaultConfig {
        applicationId "com.hegocre.nextcloudpasswords"
        minSdk 26
        targetSdk 36
        versionCode 38
        versionName "1.0.12"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }

        applicationVariants.configureEach { variant ->
            variant.resValue "string", "version_name", variant.versionName
            variant.resValue "string", "version_code", variant.versionCode.toString()
        }
    }

    androidResources {
        generateLocaleConfig = true
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    splits {
        abi {
            def isBuildingBundle = gradle.startParameter.taskNames.any { it.toLowerCase().contains("bundle") }
            enable = !isBuildingBundle
            universalApk = true
        }
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'dev.spght:encryptedprefs-ktx:1.1.1'
    implementation 'androidx.datastore:datastore-preferences:1.2.0'

    implementation 'com.squareup.okhttp3:okhttp:5.3.2'

    implementation 'com.goterl:lazysodium-android:5.2.0@aar'
    implementation 'net.java.dev.jna:jna:5.18.1@aar'

    implementation 'org.commonmark:commonmark:0.27.0'
    implementation 'io.coil-kt:coil-compose:2.7.0'

    //Compose dependencies
    implementation platform('androidx.compose:compose-bom:2025.12.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.foundation:foundation'

    implementation 'androidx.navigation:navigation-compose:2.9.6'
    implementation 'androidx.activity:activity-compose:1.12.2'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.autofill:autofill:1.3.0'
    implementation 'androidx.work:work-runtime-ktx:2.11.0'

    //Room dependencies
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'

    implementation 'com.materialkolor:material-kolor:4.0.5'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation platform('androidx.compose:compose-bom:2025.12.01')
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

// Compiler flag to use experimental Compose APIs
tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.addAll("-opt-in=kotlin.RequiresOptIn", "-Xjvm-default=all-compatibility")
    }
}